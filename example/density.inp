* output water distribution  
* first file @FF last file @LF trj @TRJ dens @dens
*

stream rtfprm.str
stream psfcrd.str
stream cryst.str

! determine water segment name
define tmp sele resid 1 .and. type OH2 end
set wseg = ?SELSEGI

! compute derived values
calc n = 1 + ( @LF - @FF )
calc mxt = @N * 1000
calc mxa = ?NATOM 
define tmp sele atom @WSEG * OH2 end
set nw = ?NSEL
calc mxs = 2 + @NW

! start correl
correl maxt @MXT maxs @MXS maxa @MXA noupdate

! cell height
enter ccel cell C

! water molecule c.o.m. Z
set k = 1
label enwat
 enter w@K atom z sele atom @WSEG @K * end mass
 incr k by 1
if k le @NW goto enwat

! open traj files
set k = @FF
set u = 101
label openlp
 open unit @U read file name @TRJ@K.trj
 incr u by 1
 incr k by 1
if k le @LF goto openlp

! fill TQ array with data requested in ENTER statements
traj firstu 101 nunit @N

! get avg cell height
show ccel
calc avheight = ?AVER
calc mxz = 0.5 * ?AVER
calc dz = 0.01 * ?AVER

! compute water distributions
set iw = 1
enter pav zero
edit pav total 100 skip 1 delta @DZ offset -@MXZ
label distloop
 mantim w@IW ratio ccel
 mantim w@IW mult @avheight
 mantim w@IW hist -@MXZ @MXZ 100
 mantim pav add w@IW
incr iw by 1
if iw le @NW goto distloop
mantim pav divi @NW
mantim pav mult 1000000

open unit 2 write card name @DENS
write pav dumb time unit 2
* dummy
*
end

stop
